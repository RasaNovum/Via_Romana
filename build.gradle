plugins {
	id "maven-publish"
	id 'com.github.johnrengelman.shadow' version '8.1.1'
	alias libs.plugins.loom
	alias libs.plugins.minotaur
}

// True for Fabric jar, False for NeoForge jar, scuffed but we ball
ext.fabric = false

if (fabric) version = "$baseVersion+$compatibleVersions-fabric"
else version = "$baseVersion+$compatibleVersions-neoforge"

archivesBaseName = project.slug

repositories {
	maven { url "https://repo.sleeping.town/" }
	maven { url "https://maven.terraformersmc.com/" }
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://api.modrinth.com/maven" }
	maven { url "https://maven.parchmentmc.org" }
	maven { url "https://raw.githubusercontent.com/SolidBlock-cn/mvn-repo/main" }
	mavenCentral()
}

dependencies {
	minecraft libs.mc

	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-1.21.1:2024.11.17@zip")
	}

	modImplementation libs.fl
	modImplementation libs.fapi

	modImplementation libs.surveyor
	include libs.surveyor

	modImplementation libs.midnightlib
	if (fabric) include libs.midnightlib

	modImplementation libs.brrp
	if (fabric) include libs.brrp
	if (!fabric) shadow(libs.brrp) { transitive = false }

	modCompileOnly libs.supplementaries
	modCompileOnly libs.iris

	modImplementation "com.google.code.gson:gson:2.10.1"
}

processResources {
	final Map<String, String> meta = [
		version       : version,
		modId         : modId,
		modName       : modName,
		modDescription: modDescription,
		homepage      : homepage,
		issues        : "${homepage}/issues",
		sources       : homepage,
		license       : license,
		authors       : authors,
		contributors  : contributors,
		members       : "${authors}${contributors.isEmpty() ? '' : '. Contributions by ' + contributors}",
		mc            : libs.versions.mc.get(),
		fl            : libs.versions.fl.get(),
		fapi          : libs.versions.fapi.get(),
		forgeFapi     : libs.versions.forgeFapi.get(),
		neoforge      : libs.versions.neoforge.get(),
		surveyor      : libs.versions.surveyor.get(),
		midnightlib   : libs.versions.midnightlib.get(),
		brrp		  : libs.versions.brrp.get(),
		forgeMidnightlib: 		libs.versions.forgeMidnightlib.get(),
		supplementaries: 		libs.versions.supplementaries.get(),
		forgeSupplementaries: 	libs.versions.forgeSupplementaries.get(),
	]
	inputs.properties(meta)
	filesMatching("*.mod.json") { expand(meta) }
	if (!fabric) filesMatching("META-INF/*neoforge.mods.toml") { expand(meta) }
}

loom {
	accessWidenerPath = file("src/main/resources/${modId}.accesswidener")

    mixin {
        defaultRefmapName = "${modId}.refmap.json"
    }

	runConfigs {
		client {
			vmArg "-Dsodium.checks.issue2561=false"
			programArg "--username=Dev"
			runDir "run/client"
		}
		server {
			runDir "run/server"
		}
	}
}

shadowJar {
	configurations = [project.configurations.shadow]
	archiveClassifier = "dev-shadow"

	// Using modified BRRP (MPL-2.0) with recipe mixins filtered to fix Sinytra Connector crash - https://github.com/SolidBlock-cn/BRRP	
	dependencies {
		include(dependency('pers.solid:brrp-fabric'))
	}
	
	doLast {
		def jarFile = archiveFile.get().asFile
		def tempDir = File.createTempDir()
		
		copy {
			from zipTree(jarFile)
			into tempDir
		}
		
		// Modify the mixins.json
		def mixinsFile = new File(tempDir, 'brrp-v1-recipes.mixins.json')
		if (mixinsFile.exists()) {
			def originalContent = mixinsFile.text
			def modifiedContent = originalContent
				.replaceAll(/\s*"CookingRecipeJsonBuilderMixin",?\s*\n?/, '')
				.replaceAll(/\s*"ShapedRecipeJsonBuilderMixin",?\s*\n?/, '')
				.replaceAll(/\s*"ShapelessRecipeJsonBuilderMixin",?\s*\n?/, '')
			mixinsFile.text = modifiedContent
		}
		
		// Modify the refmap.json
		def refmapFile = new File(tempDir, 'brrp-v1-common-refmap.json')
		if (refmapFile.exists()) {
			def originalContent = refmapFile.text
			def modifiedContent = originalContent
				.replaceAll(/"pers\/solid\/brrp\/v1\/recipe\/mixin\/CookingRecipeJsonBuilderMixin"\s*:\s*\{[^}]*\},?\s*\n?/, '')
				.replaceAll(/"pers\/solid\/brrp\/v1\/recipe\/mixin\/ShapedRecipeJsonBuilderMixin"\s*:\s*\{[^}]*\},?\s*\n?/, '')
				.replaceAll(/"pers\/solid\/brrp\/v1\/recipe\/mixin\/ShapelessRecipeJsonBuilderMixin"\s*:\s*\{[^}]*\},?\s*\n?/, '')
			refmapFile.text = modifiedContent
		}
		
		delete jarFile
		ant.jar(destfile: jarFile) {
			fileset(dir: tempDir)
		}
		
		delete tempDir
	}
}

remapJar {
	dependsOn shadowJar
	inputFile = shadowJar.archiveFile
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 21
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

sourceSets {
	main {
		resources {
			srcDirs += [
				'src/main/generated'
			]
		}
	}
}

jar {
	from("LICENSE") {
		rename { "${it}_${archivesBaseName}" }
	}
}

modrinth {
	token = "$System.env.MODRINTH_TOKEN"
	projectId = modrinthSlug
	versionNumber = project.version
	uploadFile = remapJar
	gameVersions = compatibleVersions.split(", ").toList()
	loaders = compatibleLoaders.split(", ").toList()
	changelog = !file("CHANGELOG.md").exists() ? "" : rootProject.file("CHANGELOG.md").text
	syncBodyFrom = rootProject.file("README.md").text
	dependencies {
		required.version libs.fapi.get().getName(), libs.versions.fapi.get()
		required.version libs.surveyor.get().getName(), libs.versions.surveyor.get()
		optional.version libs.supplementaries.get().getName(), libs.versions.supplementaries.get()
	}
}